#!/usr/local/bin/fontforge -script

_scale_percentage = 97;
scaled_modules = Array(2)
scaled_modules[0] = "latin_proportional1"
scaled_modules[1] = "latin_proportional2"

hex=Array(16)
hex[0]= "0"; hex[1]= "1"; hex[2]= "2"; hex[3] = "3";
hex[4]= "4"; hex[5]= "5"; hex[6]= "6"; hex[7] = "7";
hex[8]= "8"; hex[9]= "9"; hex[10]="a"; hex[11]= "b";
hex[12]="c"; hex[13]="d"; hex[14]="e"; hex[15]= "f";

# create font
New()
Reencode("unicode")
# ScaleToEm(880, 120)

_weight = Strsub($1:r, Strrstr($1:r,"-")+1)
_ttfname = $1

modules = Array($argc-2);
i = 0; while (i < SizeOf(modules))
	modules[i] = $argv[i+2]
	i++
endloop

# load SVG files in each module
mod = 0; while (mod < SizeOf(modules))
	subdir_found = 0;
	moddir = "../../../splitted/" + _weight + "/" + modules[mod]
	x = 0; while (x <= 0xf)
		y = 0; while (y <= 0xf)
			subdir = moddir + "/u" + hex[x] + hex[y]
			if (FileAccess(subdir) == 0)
				subdir_found = 1
				baseline = subdir + "/baseline"
				if (FileAccess(baseline) == 0)
					Save("tmp.sfd"); Close(); Open("tmp.sfd")
				endif
				$trace=0; Import(subdir + "/u*.svg"); $trace=0
				if (FileAccess(baseline) == 0)
					blh = LoadStringFromFile(baseline)
					SelectChanged()
					Move(0, StrTol(blh))
					i = 0; while (i < SizeOf(scaled_modules))
						if (modules[mod] == scaled_modules[i])
							Scale(_scale_percentage)
						endif			
					i++; endloop
				endif
			endif
		y++; endloop
	x++; endloop
	if (!subdir_found)
		/* AD HOC change. I'll fix soon */
			if (modules[mod] == "latin_proportional2") 
				Select("3"); Clear()
				Select("M"); Clear()
			endif
		/*   end  of  AD  HOC  change   */
		baseline = moddir + "/baseline"
		if (FileAccess(baseline) == 0)
			Save("tmp.sfd"); Close(); Open("tmp.sfd")
		endif
		$trace=0; Import(moddir + "/u*.svg"); $trace=0
		if (FileAccess(baseline) == 0)
			blh = LoadStringFromFile(baseline)
			SelectChanged(0)
			Move(0, Strtol(blh))
			i = 0; while (i < SizeOf(scaled_modules))
				if (modules[mod] == scaled_modules[i])
					Scale(_scale_percentage)
				endif
			i++; endloop
		endif
	endif
mod++; endloop

set_fontnames()
set_bearings()
set_kernings()

SelectAll(); RemoveOverlap(); RoundToInt()

Generate(_ttfname, "", 0x94)

# Punctuations are interpreted as 'DFLT' script when they are kerned normally,
# but when it is read from "kern" table, they are interpreted as 'latn'.
#Close(); Open(_ttfname)
#Generate(_ttfname, "", 0x84)

Quit()
