#!/usr/local/bin/fontforge -script

_kanji_scale_percentage = 98
kanjiclass = ["k","j","m","h","x","y","g","s","l10","l20","l21"]

digit = ["0","1","2","3","4","5","6","7","8","9"]

# create font
New()
Reencode("unicode4")
ScaleToEm(860, 140)

_weight = Strsub($1:r, Strrstr($1:r,"-")+1)
_ttfname = $1

modules = Array($argc-2);
i = 0; while (i < SizeOf(modules))
    modules[i] = $argv[i+2]
    i++
endloop

# load SVG files in each module
mod = 0; while (mod < SizeOf(modules))
    moddir = "../../../splitted/" + _weight + "/" + modules[mod]
    if (modules[mod] == "kanji")
	x = 0; while (x < SizeOf(kanjiclass))
	    y = 0; while (y < 10)
	        subdir = moddir + "/" + kanjiclass[x] + digit[y]
	        if (FileAccess(subdir) == 0)
		    $trace=0; Import(subdir + "/u*.svg"); $trace=0
		endif
	    y++; endloop
	x++; endloop
    else
	$trace=0; Import(moddir + "/u*.svg"); $trace=0
    endif
mod++; endloop

set_fontnames()
set_bearings()
set_kernings()
set_vert_chars()

if (_kanji_scale_percentage != 100)
    if (Strstr(_ttfname, "mplus-2") == 0)
	SelectWorthOutputting()
	foreach
	    u = GlyphInfo("Unicode")
	    if (u >= 0u4e00 && u <= 0x9eff)
		Scale(_kanji_scale_percentage, $em/2,($ascent+$descent)/2)
		SetWidth($em)
	    endif
	endloop
    endif
endif

Save(_ttfname:r + ".sfd")
SelectAll(); RemoveOverlap(); RoundToInt()

set_ligatures()
set_instructions()

Generate(_ttfname, "", 0x84)

Quit()
